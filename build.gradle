plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.4.1'
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
}

sourceCompatibility = 1.8

jgitver {
  mavenLike = true
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'http://first.wpi.edu/FRC/roborio/maven/release'
        }
    }
}

dependencies {
    compile group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2018.+'
    // We only need the desktop version right now, since this build file is only used for local testing.
    compile group: 'edu.wpi.first.ntcore', name: 'ntcore-java', version: '4.+'
    compile group: 'edu.wpi.first.wpiutil', name: 'wpiutil-java', version: '3.+'
    compile group: 'org.opencv', name: 'opencv-java', version: '3.2.0'
    compile group: 'edu.wpi.first.cscore', name: 'cscore-java', version: '1.3.+'
    
    runtime group: 'edu.wpi.first.wpilibj', name: 'wpilibj-jni', version: '2018.+', classifier: 'all'
    runtime group: 'edu.wpi.first.ntcore', name: 'ntcore-jni', version: '4.+', classifier: 'all'
    runtime group: 'org.opencv', name: 'opencv-jni', version: '3.2.0', classifier: 'all'
    runtime group: 'edu.wpi.first.cscore', name: 'cscore-jni', version: '1.3.+', classifier: 'all'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.google.guava', name: 'guava', version: '19.0'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description 'Builds a jar file full of sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description 'Builds a jar file full of javadocs.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
	mavenPublication(MavenPublication) {
	    from components.java
	    artifact sourcesJar {
		classifier 'sources'
	    }
	    artifact javadocJar {
		classifier 'javadoc'
	    }
	    
	    groupId 'org.hyperonline'
	    artifactId 'hyperlib'
	    
	    pom {
		name = 'hyperLib'
		description = 'Enhancements to WPILib'
		url = 'https://teamhyper.github.io/hyperLib'
		developers {
		    developer {
			id = 'jhagborg'
			name = 'James Hagborg'
			email = 'jameshagborg@gmail.com'
		    }
		}
		scm {
		    url = 'https://github.com/teamhyper/hyperLib'
		}
	    }
	}
    }
}


// By default, checkstyle wants to look at tests
// we override to only look at the main code
checkstyle {
    toolVersion = '6.11.2'
    sourceSets = [project.sourceSets.main]
}

wrapper {
    gradleVersion = 4.9
}
