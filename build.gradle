plugins {
    id 'fr.brouillard.oss.gradle.jgitver' version '0.4.1'
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
}

sourceCompatibility = 1.8

jgitver {
  mavenLike = true
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'http://first.wpi.edu/FRC/roborio/maven/release'
        }
    }
}

def WPILIB_VER = '2019.1.1-beta-1'
def OPENCV_VER = '3.4.3-15'
def JUNIT_VER = '5.3.1'

dependencies {
    // We may or may not need to add the new HAL library which was split off
    // from wpilib.  Ideally we don't have to touch it at all.
    compile "edu.wpi.first.wpilibj:wpilibj-java:$WPILIB_VER"
    compile "edu.wpi.first.ntcore:ntcore-java:$WPILIB_VER"
    compile "edu.wpi.first.wpiutil:wpiutil-java:$WPILIB_VER"
    compile "edu.wpi.first.cscore:cscore-java:$WPILIB_VER"
    compile "edu.wpi.first.cameraserver:cameraserver-java:$WPILIB_VER"
    compile "edu.wpi.first.thirdparty.frc2019.opencv:opencv-java:$OPENCV_VER"

    // Do not require these as runtime dependencies, since WPILib doesn't.
    // The user can figure out what version they need (RIO vs. desktop).
    testRuntime "edu.wpi.first.ntcore:ntcore-jni:$WPILIB_VER:all"
    testRuntime "edu.wpi.first.cscore:cscore-jni:$WPILIB_VER:all"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$JUNIT_VER"
    
    testCompile "org.junit.jupiter:junit-jupiter-api:$JUNIT_VER"
    testCompile "org.junit.jupiter:junit-jupiter-params:$JUNIT_VER"
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.google.guava:guava:19.0'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description 'Builds a jar file full of sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description 'Builds a jar file full of javadocs.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            
            groupId 'org.hyperonline'
            artifactId 'hyperlib'
            
            pom {
                name = 'hyperLib'
                description = 'Enhancements to WPILib'
                url = 'https://teamhyper.github.io/hyperLib'
                developers {
                    developer {
                        id = 'blucoat'
                        name = 'James Hagborg'
                        email = 'jameshagborg@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/teamhyper/hyperLib'
                }
            }
        }
    }
}


// By default, checkstyle wants to look at tests
// we override to only look at the main code
checkstyle {
    toolVersion = '6.11.2'
    sourceSets = [project.sourceSets.main]
}

wrapper {
    gradleVersion = '4.10.2'
}
