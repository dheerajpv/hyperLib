plugins {
  id "fr.brouillard.oss.gradle.jgitver" version "0.2.0"
}

apply plugin: 'java'
apply plugin: 'checkstyle'

sourceCompatibility = 1.8

jgitver {
  mavenLike = true
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'http://first.wpi.edu/FRC/roborio/maven/release'
        }
    }
}

dependencies {
    compile group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2018.+'
    // We only need the desktop version right now, since this build file is only used for local testing.
    compile group: 'edu.wpi.first.ntcore', name: 'ntcore-java', version: '4.+'
    compile group: 'edu.wpi.first.wpiutil', name: 'wpiutil-java', version: '3.+'
    
    runtime group: 'edu.wpi.first.ntcore', name: 'ntcore-jni', version: '4.+', classifier: 'all'
    
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'com.google.guava', name: 'guava', version: '19.0'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description 'Builds a jar file full of sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description 'Builds a jar file full of javadocs.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// If we decide to use gradle to publish archives at some point, this is how we do it
artifacts {
    archives sourcesJar
    archives javadocJar
}

// By default, checkstyle wants to look at tests
// we override to only look at the main code
checkstyle {
    toolVersion = '6.11.2'
    sourceSets = [project.sourceSets.main]
}
